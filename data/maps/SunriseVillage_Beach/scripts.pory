mapscripts SunriseVillage_Beach_MapScripts {
	MAP_SCRIPT_ON_TRANSITION {
		if (!flag(FLAG_HIDE_SUNRISE_BEACH_RIVAL)) {
			call(Common_EventScript_SetupRivalGfxId)
		}
	}
}

const LOCALID_RIVAL = 1

script SunriseVillage_Beach_EventScript_RivalSpeech {
    lock
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, Common_Movement_ExclamationMark)
	waitmovement(0)
	applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
	waitmovement(0)
	faceplayer
	speakername("{RIVAL}")
	msgbox(format(
		"{PLAYER}! Ya made it!\p"
		"Come on, sit down next to me!"
	))
	closemessage

	// Player sits next to rival
	switch(var(VAR_FACING)) {
		case DIR_SOUTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromTop)
		case DIR_NORTH:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromBottom)
		case DIR_WEST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromRight)
		case DIR_EAST:
			applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_GoNextToRivalFromLeft)
	}
	waitmovement(0)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSitsOnBench)
	waitmovement(0)

	speakername("{RIVAL}")
	msgbox(format(
		"Remember when we were little and we used to fight each other to be the first to sit on this bench?\p"
		"You always won!\p"
		"…"
	))
	closemessage

	applymovement(LOCALID_RIVAL, Common_Movement_Delay48)
	waitmovement(0)

	speakername("{RIVAL}")
	msgbox(format(
		"Hey, look!\p"
		"You can see Yifu City very clearly today, it's gorgeous!\p"
		"…\p"
		"I guess I'm feeling a little nervous about all this.\p"
		"The Pokémon getting restless in the Ginko Woods…\p"
		"Having my own Pokémon to care for…\p"
		"It feels like everything's about to change, though I'm not sure how yet."
	))
	closemessage

	delay(48)
	// They face each other
	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)

	speakername("{RIVAL}")
	msgbox(format(
		"Look at me rambling! I'm sure everything is going to be just fine.\p"
		"Are you excited to become a Pokémon wielder?"
	))

	dynmultichoice(
		16,
		6,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"I'm thrilled!",
		"I'm terrified!",
		"It's whatever."
	)

	speakername("{RIVAL}")
	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"That's the spirit! Let's become the best wielders ever!"
			))
		case 1:
			msgbox(format(
				"Don't be silly, you dumdum! I know you'll become super strong in no time!"
			))
		case 2:
			msgbox(format(
				"Oh come on, I know you're just pretending not to be excited!\p"
				"This is going to be fun, so cheer up!"
			))
	}
	closemessage

	delay(48)

	// Volcano quake!
	setvar(VAR_0x8004, 1)  // vertical pan
	setvar(VAR_0x8005, 1)  // horizontal pan
	setvar(VAR_0x8006, 20)  // num shakes
	setvar(VAR_0x8007, 5)  // shake delay
	special(ShakeCamera)
	delay(16)
	playse(SE_PIN)
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSurprisedByQuake)
	delay(16)
	playse(SE_PIN)
	applymovement(OBJ_EVENT_ID_PLAYER, SunriseVillage_Beach_Movement_PlayerSurprisedByQuake)
	waitstate
	waitmovement(OBJ_EVENT_ID_PLAYER)
	waitmovement(LOCALID_RIVAL)
	delay(48)

	// They face each other
	turnobject(OBJ_EVENT_ID_PLAYER, DIR_SOUTH)
	applymovement(LOCALID_RIVAL, Common_Movement_WalkInPlaceFasterUp)
	waitmovement(0)

	speakername("{RIVAL}")
	msgbox(format(
		"You alright, {PLAYER}?"
	))

	dynmultichoice(
		16,
		6,
		TRUE,
		10,
		0,
		DYN_MULTICHOICE_CB_NONE,
		"What was that?!",
		"Fine and dandy!"
	)

	switch (var(VAR_RESULT)) {
		case 0:
			msgbox(format(
				"The volcano's acting up again!\p"
				"Whew, the quake was bigger this time…\p"
				"… … …"
			))
		case 1:
			msgbox(format(
				"Whew! Volcano's acting up again, huh…\p"
				"The quake was bigger this time…\p"
				"… … …"
			))
	}
	closemessage

	delay(48)

	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalSeesDistribution)
	waitmovement(0)

	speakername("{RIVAL}")
	msgbox(format(
		"Oh Shuckles, the distribution's already started! Let's go to Elder Kaba's house!\p"
		"Race ya there!!"
	))
	closemessage

	turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
	// Rival runs towards the village
	applymovement(LOCALID_RIVAL, SunriseVillage_Beach_Movement_RivalRaces)
	waitmovement(0)

	setflag(FLAG_HIDE_SUNRISE_BEACH_RIVAL)
	clearflag(FLAG_HIDE_SUNRISE_VILLAGE_STANDARD_NPC_POSITIONS)
	clearflag(FLAG_HIDE_BIGSIS_HOME)
	setvar(VAR_SUNRISE_VILLAGE_INTRO_STATE, 4)

	removeobject(LOCALID_RIVAL)
    release
    end
}

movement SunriseVillage_Beach_Movement_PlayerSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_right
	delay_16
	face_down
	delay_16
	face_left
	delay_16
	face_up
}

movement SunriseVillage_Beach_Movement_RivalSurprisedByQuake {
	emote_exclamation_mark
	delay_16
	face_down
	delay_16
	face_up
	delay_16
	face_left
	delay_16
	face_right
	delay_16
	face_up
	delay_16
	face_down
}

// Corsola
script SunriseVillage_Beach_EventScript_Corsola {
	lock
    faceplayer
    waitse
    playmoncry(SPECIES_CORSOLA, CRY_MODE_NORMAL)
    msgbox(format(
        "Krr!"
    ))
    waitmoncry
    release
    end
}

script SunriseVillage_Beach_EventScript_Corsola2 {
	lock
    faceplayer
    waitse
    playmoncry(SPECIES_CORSOLA, CRY_MODE_WEAK)
    msgbox(format(
        "Laaa!"
    ))
    waitmoncry
    release
    end
}

movement SunriseVillage_Beach_Movement_RivalSitsOnBench {
	walk_in_place_faster_left
	delay_16 * 5
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromLeft {
	walk_up
	walk_right
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromTop {
	walk_in_place_faster_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromRight {
	walk_up
	walk_left
	step_end
}

movement SunriseVillage_Beach_Movement_GoNextToRivalFromBottom {
	walk_right
	walk_up * 2
	walk_left
	step_end
}

movement SunriseVillage_Beach_Movement_RivalSeesDistribution {
	face_right
	delay_16
	delay_16
	delay_16
	face_player
	step_end
}

movement SunriseVillage_Beach_Movement_RivalRaces {
	walk_fast_right * 9
	step_end
}
